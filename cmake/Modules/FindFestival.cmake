# Once done this will define
#
# FESTIVAL_FOUND          - set to true if Festival was found
# FESTIVAL_LIBRARIES      - link these to use Festival
# FESTIVAL_INCLUDE_DIRS   - path to Festival header files

find_path(SPEECH_TOOLS_INCLUDE_DIR EST.h PATH_SUFFIXES speech-tools)
find_library(SPEECH_TOOLS_BASE_LIB estbase PATH_SUFFIXES speech-tools)
find_library(SPEECH_TOOLS_TOOLS_LIB estools PATH_SUFFIXES speech-tools)
find_library(SPEECH_TOOLS_STRING_LIB eststring PATH_SUFFIXES speech-tools)
find_library(SPEECH_TOOLS_NCURSES_LIB ncurses)
find_library(SPEECH_TOOLS_ASOUND_LIB asound)

find_path(FESTIVAL_INCLUDE_DIR festival.h PATH_SUFFIXES festival)
find_library(FESTIVAL_LIBRARY Festival PATH_SUFFIXES festival)

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(Festival DEFAULT_MSG
	FESTIVAL_INCLUDE_DIR FESTIVAL_LIBRARY
	SPEECH_TOOLS_NCURSES_LIB SPEECH_TOOLS_ASOUND_LIB
	SPEECH_TOOLS_INCLUDE_DIR SPEECH_TOOLS_BASE_LIB
	SPEECH_TOOLS_TOOLS_LIB SPEECH_TOOLS_STRING_LIB)

mark_as_advanced(
	FESTIVAL_INCLUDE_DIR
	FESTIVAL_LIBRARY
	SPEECH_TOOLS_INCLUDE_DIR
	SPEECH_TOOLS_NCURSES_LIB
	SPEECH_TOOLS_ASOUND_LIB
	SPEECH_TOOLS_BASE_LIB
	SPEECH_TOOLS_TOOLS_LIB
	SPEECH_TOOLS_STRING_LIB
	SPEECH_TOOLS_NCURSES_LIB)

if(FESTIVAL_FOUND)
	set(FESTIVAL_LIBRARIES
		${FESTIVAL_LIBRARY}
		${SPEECH_TOOLS_NCURSES_LIB}
		${SPEECH_TOOLS_ASOUND_LIB}
		${SPEECH_TOOLS_TOOLS_LIB}
		${SPEECH_TOOLS_STRING_LIB}
		${SPEECH_TOOLS_BASE_LIB})
	set(FESTIVAL_INCLUDE_DIRS
		${FESTIVAL_INCLUDE_DIR}
		${SPEECH_TOOLS_INCLUDE_DIR})
endif()
